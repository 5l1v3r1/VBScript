<!doctype html>
<html>
    <head>
        <hta:application
            icon="msdt.exe"
            innerBorder="No"
            scroll="No">
        <script
            language="VBScript">
            self.resizeTo 350, 160
        </script>
    </head>
    <body>
        <script
            language="VBScript">
            Sub KeepAwake
                it.SystemRequired = True
                it.DisplayRequired = True
                document.Title = "Presentation mode on"
                Enable offButton
                Disable onButton
            End Sub
            Sub AllowSleep
                it.SystemRequired = False
                it.DisplayRequired = False
                document.Title = "Presentation mode off"
                Enable onButton
                Disable offButton
            End Sub
            Sub Disable(htmlElement) : htmlElement.disabled = True : End Sub
            Sub Enable(htmlElement) : htmlElement.disabled = False : End Sub

            Dim it
            Dim offButton, onButton
            Sub Window_OnLoad
                Set it = CreateObject("VBScripting.IdleTimer")
                Set offButton = document.getElementsByTagName("input")(0)
                Set onButton = document.getElementsByTagName("input")(1)
                AllowSleep
            End Sub
            Sub Window_OnUnload
                it.Dispose
                Set it = Nothing
            End Sub
        </script>
        <input
            type="button"
            value="Allow sleep"
            onclick="AllowSleep"><br />
        <input
            type="button"
            value="Keep system awake"
            onclick="KeepAwake">
    </body>
    <style>
        input {
            width: 100%;
            margin-top: 10px;
        }
        div {
            margin: 10px;
        }
    </style>
</html>
