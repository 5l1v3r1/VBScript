<!doctype html>
<html>
    <head>
        <hta:application
            icon="msdt.exe"
            innerBorder="No"
            scroll="No">
        <script
            language="VBScript">
            self.resizeTo 350, 160
        </script>
    </head>
    <body>
        <script
            language="VBScript">
            Sub KeepAwake
                preventer.PreventSleep
                document.Title = "Presentation mode on"
                Enable offButton
                Disable onButton
            End Sub
            Sub AllowSleep
                preventer.AllowSleep
                document.Title = "Presentation mode off"
                Enable onButton
                Disable offButton
            End Sub
            Sub Disable(htmlElement) : htmlElement.disabled = True : End Sub
            Sub Enable(htmlElement) : htmlElement.disabled = False : End Sub

            Dim preventer
            Dim offButton, onButton
            Sub Window_OnLoad
                Set preventer = CreateObject("VBScripting.IdleTimer")
                preventer.LogOps = False
                Set offButton = document.getElementsByTagName("input")(0)
                Set onButton = document.getElementsByTagName("input")(1)
                document.Title = "Presentation mode off"
                Enable onButton
                Disable offButton
            End Sub
            Sub Window_OnUnload
                preventer.Dispose
                Set preventer = Nothing
            End Sub
        </script>
        <input
            type="button"
            value="Allow sleep"
            onclick="AllowSleep"><br />
        <input
            type="button"
            value="Keep system awake"
            onclick="KeepAwake">
    </body>
    <style>
        input {
            width: 100%;
            margin-top: 10px;
        }
        div {
            margin: 10px;
        }
    </style>
</html>
