
<!-- Pause Windows Updates to get more bandwidth. Don't forget to resume. -->

<hta:application scroll="No" icon="msdt.exe" border="None" innerborder="No" id="HtaObj">
    <body>
        <input type="button" onclick="PauseUpdates()" id="pauseButton" value="Pause" />
        <input type="button" onclick="ResumeUpdates()" id="resumeButton" value="Resume" />
        <input type="button" onclick="SelfClose()" id="closeButton" value="  x  " />
        <script language="VBScript">

            Dim wup 'WindowsUpdatePauser object

            Sub PauseUpdates
                wup.PauseUpdates
                UpdateStatus(wup.GetStatus)
            End Sub

            Sub ResumeUpdates
                wup.ResumeUpdates
                UpdateStatus(wup.GetStatus)
            End Sub

            Sub UpdateStatus(status)
                If "Metered" = status Then
                    'updates are paused
                    Enable "resumeButton"
                    SetTitle "resumeButton", "Resume Windows Updates (" & wup.GetProfileName & ")"
                    Disable "pauseButton"
                    SetTitle "pauseButton", ""
                ElseIf "Unmetered" = status Then
                    'updates are enabled
                    Enable "pauseButton"
                    SetTitle "pauseButton", "Pause Windows Updates (" & wup.GetProfileName & ")"
                    Disable "resumeButton"
                    SetTitle "resumeButton", ""
                Else
                    self.close
                End If
            End Sub

            Sub Enable(id) : document.getElementById(id).disabled = False : End Sub
            Sub Disable(id) : document.getElementById(id).disabled = True : End Sub

            Sub SetTitle(id, title) : document.getElementById(id).title = title : End Sub

            Sub SelfClose : self.close : End Sub

            Sub Window_OnLoad
                Self.ResizeTo 200, 30
                Self.MoveTo 100, 0
                With CreateObject("includer")
                    Execute(.read("WindowsUpdatesPauser"))
                End With
                Set wup = New WindowsUpdatesPauser
                document.title = wup.GetAppName
                SetTitle "closeButton", "Close the Windows Updates Pauser"
                UpdateStatus(wup.GetStatus)
            End Sub

            Sub Window_OnUnload
                wup.Class_Terminate
            End Sub
        </script>
    </body>
    <style>
        body {
            margin: 1%;
            padding-left: 1%;
            background-color: Background;
        }
        input {
            height: 100%;
        }
        #pauseButton {
            width: 37%;
        }
        #resumeButton {
            width: 47%;
        }
        #closeButton {
            width: 12%;
            background-color: red;
            border-color: red;
        }
    </style>
</hta:application>